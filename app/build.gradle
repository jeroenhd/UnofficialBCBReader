apply plugin: 'com.android.application'
//apply plugin: 'com.neenbedankt.android-apt'

// Totally not stolen from https://stackoverflow.com/questions/949314/how-to-retrieve-the-hash-for-the-current-commit-in-git
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getBranchName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "nl.jeroenhd.app.bcbreader"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 7
        versionName "Elementary " + getBranchName() + "@" + getVersionName() + ' (' + new Date().format("yyyyMMddHHmmss") + ')'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

def dbflow_version = "3.1.1"
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // DBFlow

    annotationProcessor "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow:${dbflow_version}"

    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:palette-v7:27.1.1'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.android.volley:volley:1.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.evernote:android-job:1.2.5'
    testImplementation 'org.mockito:mockito-core:2.8.9'
    androidTestImplementation 'org.testng:testng:6.9.6'
}
